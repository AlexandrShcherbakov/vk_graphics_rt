#version 460
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_ray_query : require

#include "unpack_attributes.h"

layout(binding = 0, set = 0) buffer rand_points { vec4 points[]; };
layout(binding = 1, set = 0) uniform accelerationStructureEXT m_pAccelStruct;
layout(binding = 2, set = 0) buffer out_point { vec4 out_points[]; };
layout(binding = 3, set = 0) buffer counters { uint indirect_buf[]; };
layout(binding = 4, set = 0) buffer geom { vec4 geomTriangles[]; };
layout(binding = 5, set = 0) buffer geomIndices { uvec3 indexBuffer[]; };
layout(binding = 6, set = 0) buffer perInstMats { mat4 matrices[]; };
layout(binding = 7, set = 0) buffer perInstInfo { uvec2 instInfo[]; };

// RayScene intersection with 'm_pAccelStruct'
//
bool m_pAccelStruct_RayQuery_NearestHit(const vec3 rayPos, const vec3 rayDir, float len, inout vec3 target, inout vec3 normal)
{
  rayQueryEXT rayQuery;
  rayQueryInitializeEXT(rayQuery, m_pAccelStruct, gl_RayFlagsOpaqueEXT, 0xff, rayPos.xyz, 0, rayDir.xyz, len);
  
  while(rayQueryProceedEXT(rayQuery)) { }
 

  if(rayQueryGetIntersectionTypeEXT(rayQuery, true) == gl_RayQueryCommittedIntersectionTriangleEXT)
  {    
	  float t         = rayQueryGetIntersectionTEXT(rayQuery, true);
    int instanceIdx = rayQueryGetIntersectionInstanceCustomIndexEXT(rayQuery, true);
    uint primIdx = rayQueryGetIntersectionPrimitiveIndexEXT(rayQuery, true);
    // uint idx1 = indexBuffer[primIdx + instInfo[instanceIdx].x / 3].x;
    // uint idx2 = indexBuffer[primIdx + instInfo[instanceIdx].x / 3].y;
    // uint idx3 = indexBuffer[primIdx + instInfo[instanceIdx].x / 3].z;
    vec4 p1 = (geomTriangles[(primIdx * 3 + instInfo[instanceIdx].x + 0) * 2]);
    vec4 p2 = (geomTriangles[(primIdx * 3 + instInfo[instanceIdx].x + 1) * 2]);
    vec4 p3 = (geomTriangles[(primIdx * 3 + instInfo[instanceIdx].x + 2) * 2]);

    vec2 bars     = rayQueryGetIntersectionBarycentricsEXT(rayQuery, true);

    const vec3 n1 = DecodeNormal(floatBitsToInt(p1.w));
    const vec3 n2 = DecodeNormal(floatBitsToInt(p2.w));
    const vec3 n3 = DecodeNormal(floatBitsToInt(p3.w));
    normal = normalize(n1 * bars.x + n2 * bars.y + n3 * (1 - bars.x - bars.y));

    target = rayPos + rayDir * t;
    if (dot(normal, rayDir) >= 0.0)
      return false;

    return true;
  }

  return false;
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout( push_constant ) uniform kernelArgs
{
  vec3 bmin;
  uint perFacePointsCount;
  vec3 bmax;
  float voxelSize;
  mat4 mModel;
  float time;
} kgenArgs;


void main()
{
  uint tid = uint(gl_GlobalInvocationID[0]); 
  if (tid == 0)
  {
    indirect_buf[1] = 1;
    indirect_buf[2] = 0;
    indirect_buf[3] = 0;
  }

  uvec3 voxelsExtend = uvec3(ceil((kgenArgs.bmax - kgenArgs.bmin) / kgenArgs.voxelSize));
  uint pointsCount = voxelsExtend.x * voxelsExtend.y * voxelsExtend.z * 6 * kgenArgs.perFacePointsCount;

  if (tid >= pointsCount)
    return;

  uint onSurfaceIdx = tid % kgenArgs.perFacePointsCount;
  tid /= kgenArgs.perFacePointsCount;
  uint surfaceIdx = tid % 6;
  tid /= 6;
  uint zVoxel = tid % voxelsExtend.z;
  tid /= voxelsExtend.z;
  uint yVoxel = tid % voxelsExtend.y;
  tid /= voxelsExtend.y;
  uint xVoxel = tid % voxelsExtend.x;
  tid /= voxelsExtend.x;

  vec3 voxelCenter = vec3(xVoxel, yVoxel, zVoxel) * kgenArgs.voxelSize + kgenArgs.bmin + kgenArgs.voxelSize * 0.5;
  uint axis = surfaceIdx % 3;
  float offsetSign = surfaceIdx / 3 == 0 ? -1.0 : 1.0;
  vec3 offsetMask = vec3(axis == 0 ? 1.0 : 0.0, axis == 1 ? 1.0 : 0.0, axis == 2 ? 1.0 : 0.0);
  vec3 randMask = vec3(1) - offsetMask;
  vec3 offset = offsetMask * offsetSign * kgenArgs.voxelSize * 0.5;
  vec3 sideCenter = voxelCenter + offset;

  vec3 point = points[onSurfaceIdx].xyz * randMask * kgenArgs.voxelSize + sideCenter;

  vec3 res;
  vec3 normal;
  vec3 dir = voxelCenter - point;
  float len = length(dir);
  dir /= len;
  if (m_pAccelStruct_RayQuery_NearestHit(point, dir, len * 2.f, res, normal))
  {
    float targetLen = length(res - point);
    uint targetIdx = atomicAdd(indirect_buf[0], 1);
    out_points[targetIdx * 2] = vec4(res, 1);
    out_points[targetIdx * 2 + 1] = vec4(normal, 0);
  }
  // else
  // {
  //   uint targetIdx = atomicAdd(indirect_buf[0], 1);
  //   out_points[targetIdx] = vec4(mix(point, point + dir * len * 2.f, fract(kgenArgs.time / 7.0)), 1);
  // }
}

